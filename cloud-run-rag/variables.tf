# Copyright 2025 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

variable "allowed_ip_ranges" {
  description = "The ip ranges that can call the Cloud Run service."
  type        = list(string)
  nullable    = false
  default     = ["0.0.0.0/0"]
}

variable "cloud_run_configs" {
  description = "The Cloud Run configurations."
  type = object({
    frontend = object({
      containers = optional(map(any), {
        frontend = {
          image = "us-docker.pkg.dev/cloudrun/container/hello"
        }
      })
      deletion_protection = optional(bool, true)
      ingress             = optional(string, "INGRESS_TRAFFIC_INTERNAL_LOAD_BALANCER")
      max_instance_count  = optional(number, 3)
      service_invokers    = optional(list(string), [])
      vpc_access_egress   = optional(string, "ALL_TRAFFIC")
      vpc_access_tags     = optional(list(string), [])
    })
    ingestion = object({
      containers = optional(map(any), {
        ingestion = {
          image = "us-docker.pkg.dev/cloudrun/container/hello"
        }
      })
      deletion_protection = optional(bool, true)
      ingress             = optional(string, "INGRESS_TRAFFIC_INTERNAL_ONLY")
      max_instance_count  = optional(number, 3)
      service_invokers    = optional(list(string), [])
      vpc_access_egress   = optional(string, "ALL_TRAFFIC")
      vpc_access_tags     = optional(list(string), [])
    })
  })
  nullable = false
  default = {
    frontend  = {}
    ingestion = {}
  }
}

variable "db_configs" {
  description = "The Cloud SQL configurations."
  type = object({
    availability_type = optional(string, "REGIONAL")
    database_version  = optional(string, "POSTGRES_14")
    flags             = optional(map(string), { "cloudsql.iam_authentication" = "on" })
    tier              = optional(string, "db-f1-micro")
  })
  nullable = false
  default  = {}
}

variable "enable_deletion_protection" {
  description = "Whether deletion protection should be enabled."
  type        = bool
  nullable    = false
  default     = true
}

variable "ingestion_schedule_configs" {
  description = "The configuration of the Cloud Scheduler that calls invokes the Cloud Run ingestion job."
  type = object({
    attempt_deadline = optional(string, "60s")
    retry_count      = optional(number, 3)
    schedule         = optional(string, "*/30 * * * *")
  })
  nullable = false
  default  = {}
}

variable "ip_address" {
  description = "The optional load balancer IP address. If not specified, the module will create one."
  type        = string
  default     = null
}

variable "name" {
  description = "The name of the resources. This is also the project suffix if a new project is created."
  type        = string
  nullable    = false
  default     = "gf-rrag-0"
}

variable "networking_config" {
  description = "The networking configuration."
  type = object({
    create      = optional(bool, true)
    subnet_cidr = optional(string, "10.0.0.0/24")
    subnet_id   = optional(string, "sub-0")
    vpc_id      = optional(string, "net-0")
  })
  nullable = false
  default  = {}
}

variable "project_config" {
  description = "The project configuration. Billing id and parent are mandatory if "
  type = object({
    billing_account_id = optional(string)     # if create or control equals true
    control            = optional(bool, true) # control an existing project
    create             = optional(bool, true) # create and control project
    parent             = optional(string)     # if control equals true
    prefix             = optional(string)
  })
  nullable = false
  validation {
    condition = (
      var.project_config.parent == null ||
      can(regex("(organizations|folders)/[0-9]+", var.project_config.parent))
    )
    error_message = "Parent must be of the form folders/folder_id or organizations/organization_id."
  }
}

variable "public_domains" {
  type        = list(string)
  description = "The list of domains connected to the public load balancer."
  nullable    = false
  default     = ["example.com"]
}

variable "region" {
  type        = string
  description = "The GCP region where to deploy the resources."
  nullable    = false
  default     = "europe-west1"
}
